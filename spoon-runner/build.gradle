apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'com.squareup.spoon.Main'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  kotlinOptions {
    jvmTarget = '1.8'
    apiVersion = '1.1'
    languageVersion = '1.1'
  }
}

dependencies {
  implementation project(':spoon-common')
  implementation project(':third-party:axmlparser')
  implementation deps.argparser
  implementation deps.gson
  implementation deps.commonsLang3
  implementation deps.commonsIo
  implementation deps.ddmlib
  implementation deps.animatedGifLib
  implementation deps.guava
  implementation deps.mustache
  implementation deps.lesscss
  implementation deps.kotlinStdLibJdk8
  implementation(deps.jacocoMavenPlugin) {
    exclude group: 'org.apache.maven', module: 'maven-project'
  }

  testImplementation deps.junit
  testImplementation deps.truth
}

tasks.getByName('assemble').dependsOn('shadowJar')

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

artifacts {
  archives shadowJar
}
