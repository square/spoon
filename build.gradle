buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 25,
      'buildTools': '25.0.2',

      'errorProne': '2.0.19',
  ]

  ext.deps = [
      'gson': 'com.google.code.gson:gson:2.8.0',
      'jcommander': 'com.beust:jcommander:1.35',
      'commonsLang3': 'org.apache.commons:commons-lang3:3.3.1',
      'commonsIo': 'commons-io:commons-io:2.5',
      'ddmlib': 'com.android.tools.ddms:ddmlib:25.2.0',
      'animatedGifLib': 'com.madgag:animated-gif-lib:1.2',
      'guava': 'com.google.guava:guava:21.0',
      'lesscss': 'org.lesscss:lesscss:1.3.3',
      'mustache': 'com.github.spullara.mustache.java:compiler:0.8.14',
      'jacocoMavenPlugin': 'org.jacoco:jacoco-maven-plugin:0.7.6.201602180812',

      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.31',
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
  }

  repositories {
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    jcenter()
  }

  apply plugin: 'checkstyle'
  tasks.withType(Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
  }

  if (!project.path.startsWith(':third-party')) {
    project.apply(plugin: 'net.ltgt.errorprone')
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone'
            && details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }
}
