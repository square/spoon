buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 25,
      'buildTools': '25.0.2',

      'supportLibrary': '25.3.1',
      'kotlin': '1.1.1',
      'errorProne': '2.0.19',
  ]

  ext.deps = [
      'gson': 'com.google.code.gson:gson:2.8.0',
      'argparser': 'com.xenomachina:kotlin-argparser:2.0.0',
      'commonsLang3': 'org.apache.commons:commons-lang3:3.3.1',
      'commonsIo': 'commons-io:commons-io:2.5',
      'ddmlib': 'com.android.tools.ddms:ddmlib:25.3.0',
      'animatedGifLib': 'com.madgag:animated-gif-lib:1.2',
      'guava': 'com.google.guava:guava:21.0',
      'lesscss': 'org.lesscss:lesscss:1.3.3',
      'mustache': 'com.github.spullara.mustache.java:compiler:0.8.14',
      'jacocoMavenPlugin': 'org.jacoco:jacoco-maven-plugin:0.7.6.201602180812',
      'butterknife': [
          'runtime': 'com.jakewharton:butterknife:8.5.1',
          'compiler': 'com.jakewharton:butterknife-compiler:8.5.1'
      ],
      'kotlinStdLibJre8': "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",

      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.31',

      'support': [
          'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
          'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
          'design': "com.android.support:design:${versions.supportLibrary}",
          'coordinatorLayout': "com.android.support.constraint:constraint-layout:1.0.2",

          'test': [
              'espresso': 'com.android.support.test.espresso:espresso-core:2.2.2',
              'espressoContrib': 'com.android.support.test.espresso:espresso-contrib:2.2.2',
              'runner': 'com.android.support.test:runner:0.5',
              'rules': 'com.android.support.test:rules:0.5',
          ],
      ],
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }

  repositories {
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    jcenter()
  }

  apply plugin: 'checkstyle'
  project.checkstyle {
    toolVersion "7.6.1"
    configFile rootProject.file('checkstyle.xml')
  }

  if (!project.path.startsWith(':third-party')) {
    project.apply(plugin: 'net.ltgt.errorprone')
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone'
            && details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }
}
